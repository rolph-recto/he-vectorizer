input c0: [(0,15),(0,15)]
let Ix =
    for x: (0,15) {
        for y: (0,15) {
            c0[x-1][y-1] - c0[x+1][y-1] +
            2*c0[x-1][y] - (2*c0[x+1][y]) +
            c0[x-1][y+1] - c0[x+1][y+1]
        }
    }
in
let Iy =
    for x: (0,15) {
        for y: (0,15) {
            c0[x-1][y-1] + (2*c0[x][y-1]) + c0[x+1][y-1] -
            c0[x-1][y+1] - (2*c0[x][y-1]) - c0[x+1][y+1]
        }
    }
in
let Ix_2 = Ix * Ix in
let Ix_Iy = Ix * Iy in
let Iy_2 = Iy * Iy in
let Ixx =
    for x: (0,15) {
        for y: (0,15) {
            Ix_2[x][y]   +
            Ix_2[x+1][y] +
            Ix_2[x][y+1] +
            Ix_2[x+1][y+1]
        }
    }
in
let Ixy =
    for x: (0,15) {
        for y: (0,15) {
            Ix_Iy[x][y]   +
            Ix_Iy[x+1][y] +
            Ix_Iy[x][y+1] +
            Ix_Iy[x+1][y+1]
        }
    }
in
let Iyy =
    for x: (0,15) {
        for y: (0,15) {
            Iy_2[x][y]   +
            Iy_2[x+1][y] +
            Iy_2[x][y+1] +
            Iy_2[x+1][y+1]
        }
    }
in
let det =  (Ixx * Iyy) - (Ixy * Ixy) in
let trace =  Ixx + Iyy in
let scaled_det = for x: (0,15) { for y: (0,15) { 20 * det[x][y] }} in
scaled_det - (trace * trace)